resource "aws_route53_zone" "myser" {
  name = "myser.serghini.me"
}

resource "aws_acm_certificate" "boutique" {
  domain_name       = var.domain_name
  validation_method = "DNS"

  lifecycle {
    create_before_destroy = true
  }
}

resource "aws_route53_record" "cert_validation" {
  for_each = {
    for dvo in aws_acm_certificate.boutique.domain_validation_options : dvo.domain_name => {
      name   = dvo.resource_record_name
      record = dvo.resource_record_value
      type   = dvo.resource_record_type
    }
  }

  zone_id = aws_route53_zone.myser.zone_id
  name    = each.value.name
  type    = each.value.type
  records = [each.value.record]
  ttl     = 60
}

resource "aws_acm_certificate_validation" "boutique" {
  certificate_arn         = aws_acm_certificate.boutique.arn
  validation_record_fqdns = [for record in aws_route53_record.cert_validation : record.fqdn]
}

# The `boutique.myser.serghini.me` A alias is created automatically by external-dns
# (watches the Ingress + ALB), so Terraform does not manage it here.